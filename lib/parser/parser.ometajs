var Message = require('../message');
var Symbol  = require('../symbol');
var Args    = require('../args');
var Lexer   = require('./lexer').IokeLexer;

ometa LokeParser {
	program = messageChain?:mc -> mc || [],

	messageChain = expression:e seperator+ messageChain:m -> [ e ].concat(m)
	             | expression:e terminator:t seperator* messageChain:m -> [ e, t ].concat(m)
	             | terminator:t seperator* messageChain:m -> [ t ].concat(m)
	             | expression:e -> [ e ],

	expression = brackets | assoc | message,

	literal = text | int | decimal,

	message = identifier:id openParen commated:args closeParen -> new Message(id, args)
	        | identifier:id -> new Message(id, new Args())
	        | literal:id openParen commated:args closeParen -> new Message(id, args)
	        | literal:id -> new Message(id, new Args()),

	commated = keywordArg:ka whitespace* comma whitespace* commated:c -> new Args().set(ka).concat(c)
	         | keywordArg:ka -> new Args().set(ka)
	         | messageChain:mc whitespace* comma whitespace* commated:c -> new Args([ mc ]).concat(c)
	         | messageChain:mc -> new Args([ mc ])
	         | -> new Args(),

	brackets = openSquare whitespace* commated:args whitespace* closeSquare -> new Message(new Symbol('[]'), args)
	         | openCurly whitespace* commated:args whitespace* closeCurly -> new Message(new Symbol('{}'), args),

	keywordArg = identifier:id whitespace* colon whitespace* expression:e -> [ id, e ],

	assoc = expression:k whitespace* hashrocket whitespace* expression:e -> [ k, e ], // Doesn't work with lr

	whitespace = newline | seperator,

	identifier = Lexer.identifier:id -> new Symbol(id),

	seperator = Lexer.seperator,

	newline = Lexer.newline,

	openParen = Lexer.openParen,
	closeParen = Lexer.closeParen,

	openCurly = token("{"),
	closeCurly = token("}"),

	openSquare = Lexer.openSquare,
	closeSquare = Lexer.closeSquare,

	comma = Lexer.comma,

	hashrocket = Lexer.hashrocket,

	colon = Lexer.colon,

	text = token(`Text):txt -> txt,

	decimal = token(`Decimal):n -> parseFloat(n),

	int = token(`Int):n -> parseInt(n),

	terminator = (Lexer.terminator | newline) -> new Message('.', new Args)
}

/*LokeParser.prototype.token = function() {
	var current = this._current();

	if(Array.isArray(current) && current.length == 2)
		return this._skip();

	return false;
};*/